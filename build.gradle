import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.openapi.generator' version '7.2.0'
}
group = 'springboot.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_21
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

sourceSets {
	main {
		java {
			srcDirs 'src/main/java', 'build/generated/src/main/java'
		}
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'
	implementation 'org.springframework.session:spring-session-core'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
	implementation 'org.springdoc:springdoc-openapi-data-rest:1.7.0'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.1'

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	compileOnly 'org.projectlombok:lombok'
	compileOnly 'org.mapstruct:mapstruct:1.5.5.Final'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
	enabled = false
}

tasks.named('compileJava') {
	dependsOn 'openApiGenerate', 'mergeSpec'
}

openApiGenerate {
	generatorName = 'spring'
	inputSpec = "$rootDir/specs/routes.yaml"
	outputDir = "$buildDir/generated"
	apiPackage = "${group}.simpleauth.api"
	modelPackage = "${group}.simpleauth.model"
	configOptions.set([
			useSpringBoot3: "true",
			interfaceOnly : "true"
	])
}

openApiValidate {
	inputSpec = "$rootDir/specs/routes.yaml"
}

tasks.register('mergeSpec', GenerateTask) {
	generatorName = "openapi-yaml"
	inputSpec = "$rootDir/specs/routes.yaml"
	outputDir = "$rootDir/specs/out"
	configOptions.set([
			outputFile: 'openapi-gen.yaml'
	])
}


